import com.amazonaws.auth.*
import com.amazonaws.auth.profile.ProfileCredentialsProvider
import com.amazonaws.services.s3.AmazonS3Client
import com.amazonaws.services.securitytoken.AWSSecurityTokenServiceClient

apply plugin: 'java-library'
apply plugin: 'maven-publish'

group = 'com.mojang'
version = project.hasProperty('buildNumber') ? "${project.majorMinor}.${project.buildNumber}" : "${project.majorMinor}.0-SNAPSHOT"

sourceCompatibility = 17
targetCompatibility = 17

buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://libraries.minecraft.net"
        }
    }

    dependencies {
        classpath 'com.amazonaws:aws-java-sdk:1.12.172'
    }
}

repositories {
    maven {
        url "https://libraries.minecraft.net"
    }
    mavenCentral()
    maven {
        url "https://jitpack.io"
    }
}

configurations {
    jaxDoclet
}

dependencies {
    api 'com.google.code.gson:gson:2.8.9' // JsonElement and the rest used and exposed in JsonOps
    api 'com.google.guava:guava:31.0.1-jre' // TypeToken primarily exposed
    api 'it.unimi.dsi:fastutil:8.5.6' // various collection types are used in public API

    compileOnlyApi 'com.google.code.findbugs:jsr305:3.0.2'
    implementation 'org.slf4j:slf4j-api:1.8.0-beta4'
    implementation 'org.apache.commons:commons-lang3:3.12.0'

    testImplementation 'junit:junit-dep:4.10'
    testImplementation "org.slf4j:slf4j-simple:1.8.0-beta4"
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
        }
        resources {
            srcDirs = ['src/main/resources']
        }
    }
    test {
        java {
            srcDirs = ['src/test/java']
        }
        resources {
            srcDirs = ['src/test/resources']
        }
    }
}

artifacts {
    archives jar
    archives sourcesJar
}

test {
    testLogging {
        events "failed", "skipped"
        showStandardStreams = true
        showExceptions true
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
        }
    }
}

task report {
    doLast {
        println "##teamcity[buildNumber '${project.version}']"
    }
}

def publishDir = file("$buildDir/repo")


def uploadFile(s3, bucket, path, filename) {
    println "Uploading $filename to $bucket as $path"
    s3.putObject(bucket, path, filename)
}

clean.doLast {
    delete publishDir
}

if (version.endsWith("SNAPSHOT")) {
    publishing.repositories {
        mavenLocal()
    }
} else {
    publishing.repositories {
        maven {
            url "$buildDir/repo"
        }
    }

    publish.doLast {
        def AWSRoleARN = (System.getenv("AWS_ROLE_ARN") != null && System.getenv("AWS_ROLE_ARN") != "" ? System.getenv("AWS_ROLE_ARN") : null)
        if (AWSRoleARN == null) throw new GradleException("AWS Role has not been configured, use the `AWS_ROLE_ARN` environment variable")
        def providerChain = new AWSCredentialsProviderChain(
                new EnvironmentVariableCredentialsProvider(),
                new SystemPropertiesCredentialsProvider(),
                // Default provider below gets in an infinite loop if AWS_ROLE_ARN is provided
                //WebIdentityTokenCredentialsProvider.create(),
                new ProfileCredentialsProvider(),
                new EC2ContainerCredentialsProviderWrapper()
        )

        def auth = new STSAssumeRoleSessionCredentialsProvider.Builder(AWSRoleARN, "DataFixerUpperPublish")
                .withStsClient(AWSSecurityTokenServiceClient.builder().withCredentials(providerChain).build())
                .build()

        def s3 = new AmazonS3Client(auth)
        publishDir.eachFileRecurse {
            if (!it.name.contains(".xml") && !it.name.contains(".md5") && it.isFile()) {
                def relPath = publishDir.toPath().relativize(it.toPath()).toFile().toString().replaceAll('\\\\', '/')
                uploadFile(s3, "minecraft-libraries", relPath, it)
            }
        }
    }
}
